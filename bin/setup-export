#!/usr/bin/env tclsh
# Link a fossil repository (url) to a local git repository for mirroring.

package require Tcl 8.5
package require fileutil

proc main {} {
    do {*}[cmdline]
    return
}

proc cmdline {} {
    global argv
    if {[llength $argv] != 3} { usage }
    return $argv
}

proc usage {} {
    global argv0
    puts stderr "Usage: $argv0 statedir project-name giturl"
    exit 1
}

proc do {statedir project giturl} {

    # Keep in sync with setup-local, do-mirror
    set src $statedir/in
    set git $statedir/out/$project
    set trk $statedir/track

    if {[file exists $src/$project]} {
	set project [file rootname $project]
    } elseif {![file exists $src/$project.fossil]} {
	usage
    }

    puts "$project ..."

    if {[catch {
	set map [fileutil::cat $trk]
    }]} {
	set map {}
    }

    dict lappend map $project $giturl
    fileutil::writeFile $trk $map
    return
}

main
exit
